@top Sequence {
  commentLine* ~maybeComments
  IdDeclaration?
  commentLine* ~maybeComments
  ParameterDeclaration?
  commentLine* ~maybeComments
  LocalDeclaration?
  commentLine* ~maybeComments
  Metadata?
  Commands?
  ImmediateCommands?
  HardwareCommands?
}

// Potential Improvements
// flexibility - allow indentation of directives
// maintainability - use @specialize on directives
// extensibility - add generic Directive { "@"identifier }

@precedence {
  stemStart @cut
}

IdDeclaration {
  idDirective whiteSpace String whiteSpace? newLine
}

ParameterDeclaration {
  parameterDirective (whiteSpace Enum)+ whiteSpace? newLine
}

LocalDeclaration {
  localsDirective (whiteSpace Enum)+ whiteSpace? newLine
}

commandBlock {
  (Command | commentLine ~maybeComments)+
}

commentLine {
  whiteSpace? LineComment newLine
}

Commands {
  (LoadAndGoDirective whiteSpace? newLine)?
  commandBlock
}

ImmediateCommands {
  immediateDirective whiteSpace? newLine
  commandBlock
}

HardwareCommands {
  hardwareDirective whiteSpace? newLine
  commandBlock
}

TimeTag { TimeAbsolute | TimeEpoch | TimeRelative  | TimeComplete }

Args {
  (whiteSpace (arg | RepeatArgs))* whiteSpace?
}

RepeatArgs {
  "[" whiteSpace? (RepeatArg whiteSpace?)* "]"
}

RepeatArg {
  "[" whiteSpace? (arg whiteSpace?)* "]"
}

arg { Number | String | Enum }

Command {
  whiteSpace?
  TimeTag?
  Stem
  Args
  LineComment?
  newLine
  Metadata?
  Models?
}

Metadata {
  MetaEntry {
    // whiteSpace?
    metadataDirective
    whiteSpace Key { String }
    whiteSpace Value { String }
    whiteSpace? newLine
  }+
}

Models {
  Model {
    // whiteSpace?
    modelDirective
    whiteSpace Variable { String }
    whiteSpace Value { String | Number | Boolean }
    whiteSpace Offset { String }
    whiteSpace? newLine
  }+
}

Enum { identifier }

Stem { !stemStart identifier }

@tokens {
  identifier { @asciiLetter (@asciiLetter| @digit | "_" | "-")* }

  timeHhmmss { @digit@digit":"@digit@digit":"@digit@digit }

  timeDOY { @digit@digit@digit"T"timeHhmmss }

  timeSecond { $[1-9] @digit* }

  TimeAbsolute { 'A'@digit@digit@digit@digit"-"@digit@digit@digit"T"timeHhmmss whiteSpace }

  TimeRelative { 'R'(timeSecond | timeDOY | timeHhmmss) whiteSpace}

  TimeEpoch { 'E'$[+\-]?(timeSecond | timeDOY) whiteSpace}

  TimeComplete { 'C' whiteSpace }

  String { '"' (!["\\] | "\\" _)* '"' }

  hex { @digit | $[A-F] }

  Number {
    ("+" | "-")? (@digit ("_" | @digit)* ("." ("_" | @digit)*)? | "." @digit ("_" | @digit)*)
    (("e" | "E") ("+" | "-")? ("_" | @digit)+)? |
    @digit ("_" | @digit)* "n" |
    "0x" (hex | "_")+ "n"?
  }

  TRUE { "TRUE" }
  FALSE { "FALSE" }
  Boolean { TRUE | FALSE }

  LineComment { "#"![\n\r]* }

  newLine { "\n" }

  whiteSpace { $[ \t]+ }

  idDirective { "@ID" }
  LoadAndGoDirective { "@LOAD_AND_GO"}
  immediateDirective { "@IMMEDIATE" }
  hardwareDirective { "@HARDWARE" }
  localsDirective { "@LOCAL" }
  parameterDirective { "@PARAMETER" }
  metadataDirective { "@METADATA" }
  modelDirective { "@MODEL" }

  @precedence{ TimeAbsolute, TimeRelative, TimeEpoch, TimeComplete, identifier }

  @precedence {
    idDirective,
    metadataDirective,
    modelDirective,
    immediateDirective,
    hardwareDirective,
    localsDirective,
    parameterDirective,
    LoadAndGoDirective
  }
}
