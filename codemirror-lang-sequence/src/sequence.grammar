@top Sequence {
  IdDeclaration?
  ParameterDeclaration?
  LocalDeclaration?
  Metadata*
  Commands?
  ImmediateCommands?
  HardwareCommands?
}

@precedence { stemStart @cut }

IdDeclaration {
  idDirective whiteSpace String whiteSpace? newLine
}

LocalDeclaration {
  localsDirective (whiteSpace Enum)+ whiteSpace? newLine
}

ParameterDeclaration {
  parameterDirective (whiteSpace Enum)+ whiteSpace? newLine
}

commandBlock {
  (
    Command
    | whiteSpace? LineComment newLine
  )+
}

Commands {
  (LoadAndGoDirective whiteSpace? newLine)?
  commandBlock
}

ImmediateCommands {
  immediateDirective whiteSpace? newLine
  commandBlock
}

HardwareCommands {
  hardwareDirective whiteSpace? newLine
  commandBlock
}

TimeTag { TimeAbsolute | TimeEpoch | TimeRelative  | TimeComplete }

Args {
  (whiteSpace (~ambig arg | RepeatArgs))* whiteSpace? ~ambig
}

RepeatArgs {
  "[" whiteSpace? (RepeatArg whiteSpace?)* "]"
}

RepeatArg {
  "[" whiteSpace? (arg whiteSpace?)* "]"
}

arg { Number | String | Enum }

Command {
  whiteSpace? ~leadingWhitespace
  TimeTag?
  Stem
  Args
  LineComment?
  newLine
  Metadata*
  Model*
}

Metadata {
  // whiteSpace? ~leadingWhitespace
  MetadataDirective
  whiteSpace String
  whiteSpace String
  whiteSpace? newLine
}

Model {
  // whiteSpace? ~leadingWhitespace
  ModelDirective
  whiteSpace String
  whiteSpace String
  whiteSpace String
  whiteSpace? newLine
}

Enum { identifier }

Stem { !stemStart identifier }

@tokens {
  identifier { @asciiLetter (@asciiLetter| @digit | "_" | "-")* }

  timeHhmmss { @digit@digit":"@digit@digit":"@digit@digit }

  timeDOY { @digit@digit@digit"T"timeHhmmss }

  timeSecond { $[1-9] @digit* }

  TimeAbsolute { 'A'@digit@digit@digit@digit"-"@digit@digit@digit"T"timeHhmmss whiteSpace }

  TimeRelative { 'R'(timeSecond | timeDOY | timeHhmmss) whiteSpace}

  TimeEpoch { 'E'$[+\-]?(timeSecond | timeDOY) whiteSpace}

  TimeComplete { 'C' whiteSpace }

  String { '"' (!["\\] | "\\" _)* '"' }

  hex { @digit | $[A-F] }

  Number {
    ("+" | "-")? (@digit ("_" | @digit)* ("." ("_" | @digit)*)? | "." @digit ("_" | @digit)*)
    (("e" | "E") ("+" | "-")? ("_" | @digit)+)? |
    @digit ("_" | @digit)* "n" |
    "0x" (hex | "_")+ "n"?
  }

  LineComment { "#"![\n\r]* }

  newLine { "\n" | @eof }

  whiteSpace { $[ \t]+ }

  @precedence{
    TimeAbsolute,
    TimeRelative,
    TimeEpoch,
    TimeComplete,
    identifier
  }

  idDirective {"@ID" }
  // LoadAndGoDirective { @specialize<Directive, "@LOAD_AND_GO"> }
  LoadAndGoDirective { "@LOAD_AND_GO"}
  immediateDirective { "@IMMEDIATE" }
  hardwareDirective { "@HARDWARE" }
  localsDirective { "@LOCAL" }
  parameterDirective { "@PARAMETER" }
  MetadataDirective { "@METADATA" }
  ModelDirective { "@MODEL" }
  // Directive { "@" identifier }

  @precedence {
    idDirective,
    MetadataDirective,
    ModelDirective,
    immediateDirective,
    hardwareDirective,
    localsDirective,
    parameterDirective,
    LoadAndGoDirective
    // Directive
  }
}
